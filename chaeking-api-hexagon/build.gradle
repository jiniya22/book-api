plugins {
    id 'org.asciidoctor.jvm.convert' version "3.3.2"
}
sourceSets {
    main {
        resources {
            srcDirs("src/main/resources/profile/${profile}")
        }
    }
}
configurations {
    asciidoctorExtensions
}
ext {
    snippetsDir = file("build/generated-snippets")
}
dependencies {
    implementation "${boot}:spring-boot-starter-web"
    implementation "${boot}:spring-boot-starter-data-jpa"
    implementation "${boot}:spring-boot-starter-security"
    implementation "${boot}:spring-boot-starter-thymeleaf"
    implementation "${boot}:spring-boot-starter-validation"
    implementation "${cloud}:spring-cloud-starter-vault-config"
    implementation "${cloud}:spring-cloud-starter-openfeign"
    implementation 'org.springdoc:springdoc-openapi-ui:1.6.14'
    implementation "org.thymeleaf.extras:thymeleaf-extras-springsecurity5"
    implementation 'org.jsoup:jsoup:1.15.3'
    implementation 'com.auth0:java-jwt:4.2.1'
    runtimeOnly "org.mariadb.jdbc:mariadb-java-client"
    runtimeOnly 'com.h2database:h2'
    testImplementation "org.springframework.security:spring-security-test"

    asciidoctorExtensions "org.springframework.restdocs:spring-restdocs-asciidoctor"
    asciidoctorExtensions 'io.spring.asciidoctor:spring-asciidoctor-extensions-block-switch:0.6.1'
    testImplementation "org.springframework.restdocs:spring-restdocs-mockmvc"

}

dependencyManagement {
    imports {
        mavenBom "${cloud}:spring-cloud-dependencies:${springCloudVersion}"
    }
}

clean {
    delete "src/main/resources/static/docs"
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
//    systemProperty 'spring.profiles.active', 'local'
}

asciidoctor {
    configurations 'asciidoctorExtensions'
    inputs.dir snippetsDir
    dependsOn test
    baseDirFollowsSourceFile()
    doFirst {
        println(">>>> Start deleting asciidoctor html files")
        delete file('src/main/resources/static/docs')
    }
    doLast {
        println(">>>> End of asciidoctor html files deletion!")
        copy {
            from "${asciidoctor.outputDir}"
            into "src/main/resources/static/docs"
        }
    }
}

build {
    dependsOn asciidoctor
}

bootJar {
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}") {
        into "static/docs"
    }
    archiveFileName.set("chaeking-api.jar")
}